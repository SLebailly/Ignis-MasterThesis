
// Implementation of the Delta Tracking (Woordcock Tracking, Pseudo Scattering,...) null collision method
// Sample the point of the next light-medium collision along the line of flight:
// 1- determine majorant pdf to make volume homogenenous
// 2- sample a random point along the ray proportionally to said pdf
// 3- at this point, use rejection sampling to determine if the collision is with a real or fictuous particla:
//    a. If fictuous, sample next random point along the ray
//    b. If real, this is the next ray-medium intersection point

// Question 1: How to determine majorant? Is it for the whole medium or for the ray?
//      - Good question! Look for answers in papers
// Question 2: How to determine transmittance at sampled point?
//      - Calculation of transmittance is not defined in this method (at least not necessarily) but CAN be used as a stepwise function: cf. J. Novak - "Residual Ratio Trakcing for Estimating Attenuation in Participating Media" 2.3 Free-flight Sampling & Delta Tracking
fn @make_delta_tracking_medium(_ctx: ShadingContext, pms: PointMapperSet, volume: Volume, phase: PhaseFunction, _interpolate: bool) -> Medium {


    // the values that will be used for clamping the local coordinates
    let loc_min  = vec3_expand(VOLUME_BOUNDS_FLT_MIN);
    let loc_max  = vec3_expand(1 - VOLUME_BOUNDS_FLT_MIN);

    let accessor = volume.access_data(); // storing the accessor at this level to be able to leverage bottom-up traversal for nvdb trees

    /**
     * Evaluates the distance (and corresponding optical thickness) travelled along the ray before reaching the sampled transmittance value in the medium
     */
    fn @next_flight(p_start: Vec3, dir_norm: Vec3, sampled_value_opt: f32) -> (f32, Color, VolumeProperties) {

        fn @next_flight_local(local_start: Vec3, local_dir_norm: Vec3, local_sampled_value_opt: f32) -> (f32, Color, VolumeProperties) {

            let sigma_t   = volume.get_majorant_extinction(local_start, local_dir_norm);
            let sigma_t_p = vec3_max_value(color_to_vec3(sigma_t));
            let sampled_dist = local_sampled_value_opt / sigma_t_p;

            let sampled_point = vec3_add(local_start, vec3_mulf(local_dir_norm, sampled_dist));

            let (dist, local_properties) = if volume.is_in_volume(sampled_point) {
                // inside the medium
                let (props, _dim) = accessor.properties_at_indx(volume.coord_to_indx(sampled_point));
                (sampled_dist, props)
            } else {
                // leaving the medium
                (flt_max, make_vacuum_volume_properties())
            };

            (dist, color_mulf(sigma_t, sampled_dist), homogenize_nullscattering_properties(local_properties, sigma_t))
        }

        let loc_start = vec3_clamp(pms.to_normalized_point(p_start), loc_min, loc_max);
        let loc_end   = vec3_clamp(pms.to_normalized_point(vec3_add(p_start, dir_norm)), loc_min, loc_max);
        let loc_dir   = vec3_sub(loc_end, loc_start);

        let loc_dir_len2 = math_builtins::fmax(flt_eps, vec3_len2(loc_dir));

        // the difference between the normalized vector (length 1) and the vector after transformation to local coordinates is the difference in scale between the distance in local and global coordinates
        let scale_diff          = math_builtins::sqrt(loc_dir_len2);
        let (loc_dist, loc_opt, loc_properties) = if (loc_dir_len2 > flt_eps) {
            next_flight_local(loc_start, vec3_normalize(loc_dir), sampled_value_opt * scale_diff)
        } else {
            (0:f32, make_gray_color(1:f32), make_vacuum_volume_properties())
        };

        //transform the local distance and corresponding optical thickness into global distance/optical thickness
        let dist                = if loc_dist >= flt_max { flt_max } else { loc_dist / scale_diff };
        let opt                 = color_divf(loc_opt, scale_diff);

        (dist, opt, loc_properties)
    }

    /**
     * Evaluates the transmittance between two points in the medium
     */
    fn @eval_tr(p_start: Vec3, p_end: Vec3) -> Color {

        let loc_start  = vec3_clamp(pms.to_normalized_point(p_start), loc_min, loc_max);
        let loc_end    = vec3_clamp(pms.to_normalized_point(p_end),   loc_min, loc_max);
        let dir_u      = vec3_sub(p_end, p_start);
        let loc_dir    = vec3_sub(loc_end, loc_start);
        let scale_diff = math_builtins::sqrt(vec3_len2(dir_u) / math_builtins::fmax(flt_eps, vec3_len2(loc_dir)));

        let local_opt  = eval_optical_thickness(volume, loc_start, loc_end, false); //TODO: implement null-scattering transmittance estimation, atm this still uses regular tracking

        optical_thickness_to_transmittance(color_mulf(local_opt, scale_diff)) // TODO: does this need to be weighted by PDF...???
    }


    
    Medium {
        phase = @|_| phase,
        eval  = @|p_start, p_end| {
            eval_tr(p_start, p_end)
        },
        eval_inf = @|_, _| color_builtins::black,
        pdf = |_, _, _| {
            1:f32
        },
        sample = @|rnd, p_start, p_end| {

            // sample value of transmittance (uniformly)
            let sampled_value_tr = randf(rnd) * 0.99999;
            // transform the sampled value of transmittance to its corresponding optical thicknes (easier on computation for the iterator)
            let sampled_value_opt = - math_builtins::log(1:f32 - sampled_value_tr); // tr = e^(-opt) <=> opt = - log(tr)


            let dir_u    = vec3_sub(p_end, p_start);
            let max_dist = vec3_len(dir_u);
            let dir_norm = vec3_mulf(dir_u, safe_div(1, max_dist));

            // evaluate the corresponding distance for the sampled value of transmittance
            let (mut dist, opt, props) = next_flight(p_start, dir_norm, sampled_value_opt);
            // clamp the distance to a minimum distance to avoid staying in the same position
            dist = math_builtins::fmax[f32](0.0001, dist);//TODO: potentially lower eps

            // reject samples if the optical thickness is black (no transmittance) or if leaving the medium/hitting another object before interacting with the medium
            if is_black_eps(opt, 1e-3) { return(reject_medium_sample()) }
            if max_dist - dist <= 0.0001 { return(reject_medium_sample()) } //TODO: potentially lower eps


            /*
            cf. J. Novak - Monte Carlo Methods for Volumetric Light Transport Simulation (4. Distance Sampling):
            "The distance distribution strictly adheres to the Beer-Lambert law, i.e. it has a PDF proportional to the transmittance along the given ray"
            => pdf = transmittance
            */

            // determine position, transmittance and pdf based on previous calculations
            let pos   = vec3_add(p_start, vec3_mulf(dir_norm, dist));
            let trans = optical_thickness_to_transmittance(opt);
            let pdf   = sampled_value_tr;// * vec3_max_value(color_to_vec3(props.coeff_extinction));//TODO: verify this
               
            make_medium_sample(pos, pdf, color_builtins::white, props)
        },
        is_homogeneous = false
    }
}