/**
 * Marches to the bounds of the next voxel, with the ability to skip voxels when the node is sparse
 * TODO: fix error in explosion file
 */
fn @make_hdda_marcher(origin: Vec3, ndir: Vec3, volume: Volume, _interpolate: bool) -> VolumeMarcher {

    let accessor = volume.access_data();
    let t_vxl_delta = vec3_abs(vec3_safediv(volume.voxel_dimensions, ndir));

    let (vxl_indx_x, vxl_indx_y, vxl_indx_z) = volume.coord_to_indx(origin);
    let (_current_props, dim) = accessor.properties_at_indx(vxl_indx_x, vxl_indx_y, vxl_indx_z);
    let (indx_x, indx_y, indx_z) = volume.to_sparse_indx(vxl_indx_x, vxl_indx_y, vxl_indx_z, dim);
    
    let (lower_bounds, upper_bounds) = volume.bounds(indx_x, indx_y, indx_z, dim);

    // can be negative (depending on direction)
    let (step_x, t_max_x) = if (ndir.x > flt_eps) { (1, safe_div((upper_bounds.x - origin.x), ndir.x)) } else if (ndir.x < -flt_eps) { (-1, safe_div((lower_bounds.x - origin.x), ndir.x)) } else { (0, flt_max) }; //removed clamping here and added case for dir 0... TODO: verify if correct
    let (step_y, t_max_y) = if (ndir.y > flt_eps) { (1, safe_div((upper_bounds.y - origin.y), ndir.y)) } else if (ndir.y < -flt_eps) { (-1, safe_div((lower_bounds.y - origin.y), ndir.y)) } else { (0, flt_max) };
    let (step_z, t_max_z) = if (ndir.z > flt_eps) { (1, safe_div((upper_bounds.z - origin.z), ndir.z)) } else if (ndir.z < -flt_eps) { (-1, safe_div((lower_bounds.z - origin.z), ndir.z)) } else { (0, flt_max) };

    /**
     * Variables that change when switching between layers
     */
    let mut m_dim = dim;
    let mut m_t_delta = vec3_mulf(t_vxl_delta, dim as f32);

    /**
     * Variables that change during iteration
     */
    let mut m_indx_x = indx_x;
    let mut m_indx_y = indx_y;
    let mut m_indx_z = indx_z;

    let mut m_t_max   = vec3_abs(make_vec3(t_max_x, t_max_y, t_max_z));
    let mut m_t_total = 0:f32;
    let mut m_t_0     = 0:f32; //storing t_total every time we update the DDA (as t_max gets overwritten)

    fn @m_update_dimension() -> VolumeProperties {
        m_t_0 = m_t_total; // store current progress
        let eps = 0.0001:f32;
        let current_pos = vec3_add(origin, vec3_mulf(ndir, m_t_total));

        //TODO: optimize this.... There has to be a better way...?
        let flt_correction = vec3_mulf(make_vec3(step_x as f32, step_y as f32, step_z as f32), eps);
        let (lower, upper) = volume.bounds(m_indx_x, m_indx_y, m_indx_z, m_dim);
        let clamped_pos = vec3_min(vec3_max(vec3_add(current_pos, flt_correction), lower), upper);
        let (current_x, current_y, current_z) = volume.coord_to_indx(clamped_pos);

        let (props, new_dim) = accessor.properties_at_indx(current_x, current_y, current_z);
        let (new_indx_x, new_indx_y, new_indx_z) = volume.to_sparse_indx(current_x, current_y, current_z, new_dim);

        let (lower_bounds, upper_bounds) = volume.bounds(new_indx_x, new_indx_y, new_indx_z, new_dim);
       
        // can be negative (depending on direction)
        let t_max_x = if (step_x > 0) { safe_div((upper_bounds.x - current_pos.x), ndir.x) } else if (step_x < 0) { safe_div((lower_bounds.x - current_pos.x), ndir.x) } else { flt_max }; //removed clamping here and added case for dir 0... TODO: verify if correct
        let t_max_y = if (step_y > 0) { safe_div((upper_bounds.y - current_pos.y), ndir.y) } else if (step_y < 0) { safe_div((lower_bounds.y - current_pos.y), ndir.y) } else { flt_max };
        let t_max_z = if (step_z > 0) { safe_div((upper_bounds.z - current_pos.z), ndir.z) } else if (step_z < 0) { safe_div((lower_bounds.z - current_pos.z), ndir.z) } else { flt_max };

        m_dim = new_dim;
        m_t_delta = vec3_mulf(t_vxl_delta, new_dim as f32);

        m_t_max  = vec3_abs(make_vec3(t_max_x, t_max_y, t_max_z));
        m_indx_x = new_indx_x;
        m_indx_y = new_indx_y;
        m_indx_z = new_indx_z;

        props
    }

    VolumeMarcher {
        has_next     = @|| {
            m_indx_x + m_dim > 0 && m_indx_x - m_dim < volume.width - 1 && m_indx_y + m_dim > 0 && m_indx_y - m_dim < volume.height - 1 && m_indx_z + m_dim > 0 && m_indx_z - m_dim < volume.depth - 1 // check that the current super-voxel still has at least SOME voxels within it in the grid.
        },
        next_segment = @|| {
            let (local_props_for_current_dim, local_dim) = accessor.properties_at_indx(m_indx_x, m_indx_y, m_indx_z);
            let local_props = if (local_dim != m_dim) {
                stats::add_quantity(stats::Quantity::TraversalDimensionUpdates, 1);
                m_update_dimension()
            } else {
                local_props_for_current_dim
            };

            let (new_x, new_y, new_z, new_t_max) = if m_t_max.x < m_t_max.y {
                if m_t_max.x < m_t_max.z {
                    (
                        m_indx_x + (step_x * m_dim),
                        m_indx_y,
                        m_indx_z,
                        vec3_add(m_t_max, make_vec3(m_t_delta.x, 0, 0))
                    )
                } else {
                    (
                        m_indx_x,
                        m_indx_y,
                        m_indx_z + (step_z * m_dim),
                        vec3_add(m_t_max, make_vec3(0, 0, m_t_delta.z))
                    )
                }
            } else {
                if m_t_max.y < m_t_max.z {
                    (
                        m_indx_x,
                        m_indx_y + (step_y * m_dim),
                        m_indx_z,
                        vec3_add(m_t_max, make_vec3(0, m_t_delta.y, 0))
                    )
                } else {
                    (
                        m_indx_x,
                        m_indx_y,
                        m_indx_z + (step_z * m_dim),
                        vec3_add(m_t_max, make_vec3(0, 0, m_t_delta.z))
                    )
                }
            };
        
            let new_t_total = m_t_0 + vec3_min_value(m_t_max);// math_builtins::fmin(m_t_max.x, math_builtins::fmin(m_t_max.y, m_t_max.z));
            let t           = new_t_total - m_t_total;

            let segment = PathSegment {
                distance       = t,
                local_properties = local_props
            };

            // adapt mutable variables
            m_indx_x  = new_x;
            m_indx_y  = new_y;
            m_indx_z  = new_z;
            m_t_max   = new_t_max;
            m_t_total = new_t_total;
        
            stats::add_quantity(stats::Quantity::TraversalSteps, 1);
            // return
            (segment, new_t_total)
        }
    }
}
