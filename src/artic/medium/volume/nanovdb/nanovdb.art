
fn @make_nvdb_volume_f32(buffer: DeviceBuffer, principled_volume_shader: VolumeShader[PrincipledVolumeValues]) -> Volume {

    let tree = make_nvdb_tree[f32, f32](buffer, 4, 4, @|bf, i| bf.load_f32(i), @|bf, i| bf.load_f32(i));

    let ((x_min, y_min, z_min), (x_max, y_max, z_max)) = tree.access_root_node(tree.access_tree_data().get_node_address_root(true)).get_bounding_box(true);
    let width  = (x_max - x_min) + 1;
    let height = (y_max - y_min) + 1;
    let depth  = (z_max - z_min) + 1;

    fn @clamp_index(i: i32, j: i32, k: i32) -> (i32, i32, i32) {
        //clamp between 0 and width-height-depth
        let is = if i < 0 { 0 } else if i >= width  { width  - 1 } else { i };
        let js = if j < 0 { 0 } else if j >= height { height - 1 } else { j };
        let ks = if k < 0 { 0 } else if k >= depth  { depth  - 1 } else { k };

        //(is, js, ks)
        (is + x_min, js + y_min, ks + z_min)
    }

    fn @make_nvdb_accessor() -> VolumeAccessor {

        let mut last_path = Option[AccessPath]::None;

        fn @cache_path_get_value(i: i32, j: i32, k: i32) -> (f32, AccessPath) {
            if let Option[AccessPath]::Some(previous_path) = last_path {
                let (value, path) = bottom_up_get_value(tree, clamp_index(i, j, k), previous_path);
                last_path = Option[AccessPath]::Some(path);
                return(value, path)
            } else {
                let (value, path) = top_down_get_value(tree, clamp_index(i, j, k));
                last_path = Option[AccessPath]::Some(path);
                return(value, path)
            }
        }
        
        VolumeAccessor {
            properties_at_indx = @|i, j, k| {
                let (value, path) = cache_path_get_value(i, j, k);
                (principled_volume_shader.volume_properties(make_principled_volume_values(value, value)), path.sparse_dim)
            }
        }
    }

    fn @get_majorant_extinction(_origin: Vec3, _ndir: Vec3) -> Color {
        //TODO: add DDA on higher dimension to return majorant
        make_gray_color(10:f32)
    }

    make_volume(width, height, depth, make_nvdb_accessor, get_majorant_extinction)
}
