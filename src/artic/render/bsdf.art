// Result of sampling a BSDF
struct BsdfSample {
    in_dir: Vec3,  // Sampled direction
    pdf:    f32,   // BSDF value for the sample
    color:  Color, // Color of the sample, with cosine and pdf already applied
    eta:    f32    // Eta on the sampled direction
}

// Opaque BSDF structure
struct Bsdf {
    // Evaluates the reflectance of the material for the given pair of directions, the cosine term is already applied
    eval: fn (Vec3, Vec3) -> Color,
    // Evaluates the sampling probability density for a given pair of directions
    pdf: fn (Vec3, Vec3) -> f32,
    // Samples a direction
    sample: fn (&mut RndState, Vec3, bool) -> Option[BsdfSample],
    // Is true if the material is purely specular
    is_specular: bool
}

// Creates a BSDF sample and checks that it lies on the right side of the surface
fn @make_bsdf_sample(surf: SurfaceElement, in_dir: Vec3, pdf: f32, color: Color, eta: f32, inverted: bool) -> Option[BsdfSample] {
    // Checks that the sample is above the surface (or under it if inverted is true)
    let valid = (pdf > 0) && (inverted ^ (vec3_dot(in_dir, surf.face_normal) > 0));
    make_option(BsdfSample {
        in_dir = in_dir,
        pdf    = if valid { pdf } else { 1 },
        color  = if valid { color } else { color_builtins::black },
        eta    = if valid { eta } else { 1 }
    })
}

fn @reject_bsdf_sample() = Option[BsdfSample]::None;

fn @scale_bsdf_sample(sample: BsdfSample, factor: f32) = BsdfSample {
    in_dir = sample.in_dir,
    pdf    = sample.pdf * factor,
    color  = color_mulf(sample.color, 1 / factor),
    eta    = sample.eta
};