// Opaque camera object
struct Camera {
    // Generates a ray for a point on the image plane (in [-1, 1]^2)
    generate_ray: fn (f32, f32) -> Ray,
    // Return position of camera
    origin: fn() -> Vec3
}

// Creates a perspective camera
fn @make_perspective_camera(math: Intrinsics, eye: Vec3, view: Mat3x3, w: f32, h: f32) -> Camera {
    let dir   = view.col(2);
    let right = view.col(0);
    let up    = view.col(1);

    Camera {
        generate_ray = @ |x, y| {
            let d = vec3_normalize(math,
                vec3_add(
                    vec3_add(vec3_mulf(right, w * x),
                             vec3_mulf(up, h * y)),
                    dir
                )
            );
            make_ray(eye, d, 0, flt_max)
        },
        origin = @ || eye
    }
}

// Creates an orthogonal camera
fn @make_orthogonal_camera(_math: Intrinsics, eye: Vec3, view: Mat3x3, w: f32, h: f32) -> Camera {
    let dir   = view.col(2);
    let right = view.col(0);
    let up    = view.col(1);

    Camera {
        generate_ray = @ |x, y| {
            let pos = vec3_add(
                            vec3_add(vec3_mulf(right, w * x),
                                    vec3_mulf(up, h * y)),
                            eye
                      );
            make_ray(pos, dir, 0, flt_max)
        },
        origin = @ || eye
    }
}

enum FisheyeAspectMode {
    Circular,
    Cropped,
    Full
}

// Creates a fishlens camera
fn @make_fishlens_camera(math: Intrinsics, eye: Vec3, view: Mat3x3, w: f32, h: f32) -> Camera {
    let fov = flt_pi;
    let V   = mat3x3_transpose(view);
    let asp = w / h;
    
    let mode = FisheyeAspectMode::Circular;

    // Select aspect ratio mode
    let (xasp, yasp) = match(mode) {
        FisheyeAspectMode::Circular => {
            let xasp = if asp < 1 { 1 } else { asp };
            let yasp = if asp > 1 { 1 } else { asp };
            (xasp, yasp)
        },
        FisheyeAspectMode::Cropped => {
            let xasp = if asp < 1 { 1 / asp } else { 1 };
            let yasp = if asp > 1 { 1 / asp } else { 1 };
            (xasp, yasp)
        },
        FisheyeAspectMode::Full => {
            let diameter = host_sqrtf(asp * asp + 1) * h;
            let min_s	 = fmin(w, h);
            let f        = diameter / min_s;
            let xasp = if asp < 1 { f } else { f / asp };
            let yasp = if asp > 1 { f } else { f * asp };
            (xasp, yasp)
        }
    };

    Camera {
        generate_ray = @ |x, y| {
            let nx    = x * xasp;
            let ny    = y * yasp;
            let r	  = math.sqrtf(nx * nx + ny * ny);
		    let theta = r * fov / 2;

		    let sT = math.sinf(theta);
            let cT = math.cosf(theta);
            let sP = if r < flt_eps { 0 } else { ny / r };
            let cP = if r < flt_eps { 0 } else { nx / r };
            let nd = make_vec3(sT*cP, sT*sP, cT);
            let d  = vec3_normalize(math, mat3x3_mul(V, nd));
            make_ray(eye, d, 0, flt_max)
        },
        origin = @ || eye
    }
}
