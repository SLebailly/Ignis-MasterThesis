struct VPTRayPayload {
    mis:     f32,
    contrib: Color,
    depth:   i32,
    eta:     f32,
    medium:  i32,
}

fn wrap_vptraypayload(payload: VPTRayPayload) -> RayPayload {
    let mut r : RayPayload;
    r.components(0) = payload.mis;
    r.components(1) = payload.contrib.r;
    r.components(2) = payload.contrib.g;
    r.components(3) = payload.contrib.b;
    r.components(4) = payload.depth as f32;
    r.components(5) = payload.eta;
    r.components(6) = payload.medium as f32;
    r
}

fn unwrap_vptraypayload(payload: RayPayload) = VPTRayPayload {
    mis     = payload.components(0),
    contrib = make_color(payload.components(1), payload.components(2), payload.components(3), 1),
    depth   = payload.components(4) as i32,
    eta     = payload.components(5),
    medium  = payload.components(6) as i32,
};

// TODO: Add luminaire sampling (aka NEE within participating media)
fn @make_volume_path_renderer(max_path_len: i32, num_lights: i32, lights: LightTable, media: MediaTable, _aovs: AOVTable, clamp_value: f32) -> Technique {
    let offset : f32  = 0.001;
    let pdf_lightpick = if num_lights == 0 { 1 } else { 1 / (num_lights as f32) };

    let handle_color = if clamp_value > 0 {
        @|c: Color| color_saturate(c, clamp_value)
    } else {
        @|c: Color| c
    };

    fn @on_shadow( ray: Ray
                 , _pixel: i32
                 , _hit: Hit
                 , rnd: &mut RndState
                 , payload: RayPayload
                 , surf: SurfaceElement
                 , mat: Material
                 ) -> ShadowRay {
        // No shadow rays for specular materials
        if mat.bsdf.is_specular || num_lights == 0 {
            return(ShadowRay::None)
        }
        
        if unwrap_vptraypayload(payload).depth + 1 > max_path_len {
            return(ShadowRay::None)
        }

        let light_id      = pick_light(rnd, num_lights);
        let light         = @lights(light_id);
        let sample_direct = light.sample_direct;
        let light_sample  = @sample_direct(rnd, surf);
        
        let pt     = unwrap_vptraypayload(payload);
        let medium = @media(pt.medium);
        
        let hitvol = medium.eval(ray.org, surf.point);

        // TODO: Secondary rays do not take "just" media into account. Would be useful to add transmittance into account...
        if light.infinite {
            if light_sample.pdf_dir <= flt_eps {
                return(ShadowRay::None)
            }

            let light_dir = light_sample.posdir; // Infinite lights return a direction instead of a position
            let vis       = vec3_dot(light_dir, surf.local.col(2));

            if vis > flt_eps {
                let in_dir  = light_dir; 
                let out_dir = vec3_neg(ray.dir);

                let pdf_e     = if light.delta { 0 } else { mat.bsdf.pdf(in_dir, out_dir) }; // Pdf to sample the "infinite" light based on bsdf
                let pdf_l     = light_sample.pdf_dir * pdf_lightpick;                        // Pdf to sample the light based on NEE
                let inv_pdf_l = 1 / pdf_l;
                
                let mis = 1 / (1 + pdf_e * inv_pdf_l);
                
                let vol     = color_mul(hitvol, medium.eval_inf(surf.point, in_dir));
                let contrib = color_mul(light_sample.intensity, color_mul(pt.contrib, color_mul(mat.bsdf.eval(in_dir, out_dir), vol)));

                return(make_simple_shadow_ray(
                    make_ray(surf.point, light_dir, offset, flt_max),
                    handle_color(color_mulf(contrib, mis * inv_pdf_l))
                ))
            }
        } else {
            if light_sample.pdf_area <= flt_eps {
                return(ShadowRay::None)
            }

            let light_dir = vec3_sub(light_sample.posdir, surf.point);
            let vis       = vec3_dot(light_dir, surf.local.col(2));

            if vis > flt_eps && light_sample.cos > flt_eps {
                let inv_d   = 1 / vec3_len(light_dir);
                let inv_d2  = inv_d * inv_d;
                let in_dir  = vec3_mulf(light_dir, inv_d);
                let out_dir = vec3_neg(ray.dir);
                let cos_l   = light_sample.cos;

                let pdf_e     = if light.delta { 0 } else { mat.bsdf.pdf(in_dir, out_dir) * cos_l * inv_d2 };
                let pdf_l     = light_sample.pdf_area * pdf_lightpick;
                let inv_pdf_l = 1 / pdf_l;

                let mis         = 1 / (1 + pdf_e * inv_pdf_l);
                let geom_factor = inv_pdf_l * cos_l * inv_d2;
                
                let vol     = color_mul(hitvol, medium.eval(surf.point, light_sample.posdir));
                let contrib = color_mul(light_sample.intensity, color_mul(pt.contrib, color_mul(mat.bsdf.eval(in_dir, out_dir), vol)));
                
                return(make_simple_shadow_ray(
                    make_ray(surf.point, light_dir, offset, 1 - offset),
                    handle_color(color_mulf(contrib, geom_factor * mis))
                ))
            }
        }
        ShadowRay::None
    }

    fn @on_hit( ray: Ray
              , _pixel: i32
              , hit: Hit
              , payload: RayPayload
              , surf: SurfaceElement
              , mat: Material
              ) -> Option[Color] {
        let pt = unwrap_vptraypayload(payload);

        // Hits on a light source
        if mat.is_emissive && surf.is_entering {
            let medium  = @media(pt.medium);
            let out_dir = vec3_neg(ray.dir);
            let dot     = vec3_dot(out_dir, surf.local.col(2));
            if dot > flt_eps { // Only contribute proper aligned directions
                let emit     = mat.emission(out_dir);
                let next_mis = pt.mis * hit.distance * hit.distance / dot;
                let mis      = 1 / (1 + next_mis * pdf_lightpick * emit.pdf_area);
                let vol      = medium.eval(ray.org, surf.point);
                let contrib  = handle_color(color_mulf(color_mul(pt.contrib, color_mul(emit.intensity, vol)), mis));
                
                return(make_option(contrib))
            }
        }
        Option[Color]::None
    }

    fn @on_miss( ray: Ray
               , _pixel: i32
               , payload: RayPayload) -> Option[Color] {
        let mut inflights = 0;
        let mut color     = color_builtins::black;
        
        let pt     = unwrap_vptraypayload(payload);
        let medium = @media(pt.medium);

        for light_id in unroll(0, num_lights) {
            let light = @lights(light_id);
            // Do not include delta lights or finite lights
            if light.infinite && !light.delta {
                
                inflights += 1;

                let out_dir = vec3_neg(ray.dir);
                let emit    = light.emission(out_dir, make_invalid_surface_element());
                let mis     = 1 / (1 + pt.mis * pdf_lightpick * emit.pdf_dir);
                let vol     = medium.eval_inf(ray.org, ray.dir);
                color       = color_add(color, handle_color(color_mulf(color_mul(pt.contrib, color_mul(emit.intensity, vol)), mis)));
            }
        }

        if inflights > 0 {
            make_option(color)
        } else {
            Option[Color]::None
        }
    }

    fn @on_bounce( ray: Ray
                 , _pixel: i32
                 , _hit: Hit
                 , rnd: &mut RndState
                 , payload: RayPayload
                 , surf: SurfaceElement
                 , mat: Material
                 ) -> Option[(Ray, RayPayload)] {
        let pt      = unwrap_vptraypayload(payload);
        let medium  = @media(pt.medium);
        let out_dir = vec3_neg(ray.dir);

        // Try sampling the medium
        if let Option[MediumSample]::Some(medium_sample) = medium.sample(rnd, ray.org, surf.point) {
            let phase_sample = medium.phase(medium_sample.pos).sample(rnd, out_dir);
            let contrib      = color_mul(pt.contrib, color_mulf(medium_sample.color, phase_sample.weight)/* Pdf already applied!*/);
            let rr_prob      = russian_roulette_pbrt(color_mulf(contrib, pt.eta * pt.eta), 0.95);

            if pt.depth + 1 > max_path_len || randf(rnd) >= rr_prob {
                Option[(Ray, RayPayload)]::None
            } else {
                let new_contrib = color_mulf(contrib, 1 / rr_prob);
                //let mis         = 1 / (medium_sample.pdf * phase_sample.pdf);
                make_option(
                    make_ray(medium_sample.pos, phase_sample.in_dir, 0, flt_max),
                    wrap_vptraypayload(VPTRayPayload {
                        mis     = 0/*mis*/,
                        contrib = new_contrib,
                        depth   = pt.depth + 1,
                        eta     = pt.eta,
                        medium  = pt.medium
                    })
                )
            }
        } else {
            // Bounce on surface
            if let Option[BsdfSample]::Some(mat_sample) = mat.bsdf.sample(rnd, out_dir, false) {
                let vol         = medium.eval(ray.org, surf.point);
                let vol_contrib = color_mul(vol, pt.contrib);
                let rr_prob     = russian_roulette_pbrt(color_mulf(vol_contrib, pt.eta * pt.eta), 0.95);
                if pt.depth + 1 > max_path_len || randf(rnd) >= rr_prob {
                    return(Option[(Ray, RayPayload)]::None)
                }
                
                let contrib     = color_mul(vol_contrib, mat_sample.color/* Pdf and cosine are already applied!*/);
                let mis         = if mat.bsdf.is_specular { 0 } else { 1 / mat_sample.pdf };
                let new_contrib = color_mulf(contrib, 1 / rr_prob);
                
                let is_transmission = math_builtins::signbit(vec3_dot(surf.local.col(2), mat_sample.in_dir));
                let new_medium      = if is_transmission { mat.interface.pick(surf.is_entering) } else { pt.medium };
                make_option(
                    make_ray(surf.point, mat_sample.in_dir, offset, flt_max),
                    wrap_vptraypayload(VPTRayPayload {
                        mis     = mis,
                        contrib = new_contrib,
                        depth   = pt.depth + 1,
                        eta     = pt.eta * mat_sample.eta,
                        medium  = new_medium
                    })
                )
            } else {
                Option[(Ray, RayPayload)]::None
            }
        }
    }

    Technique {
        on_hit         = on_hit,
        on_miss        = on_miss,
        on_shadow      = on_shadow,
        on_bounce      = on_bounce,
        on_shadow_hit  = TechniqueNoShadowHitFunction,
        on_shadow_miss = TechniqueNoShadowMissFunction,
    }
}
