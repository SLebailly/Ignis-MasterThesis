// Returns the probability to continue given the contribution of a path
fn @russian_roulette(c: Color, clamp: f32) = clampf(2 * color_luminance(c), 0.05, clamp);

// Russian roulette used in pbrt v4
fn @russian_roulette_pbrt(c: Color, clamp: f32) = clampf(color_max_component(c), 0.05, clamp);

struct PTRayPayload {
    inv_pdf: f32,   // Inverse BSDF pdf
    contrib: Color, // Current contribution
    depth:   i32,   // Current depth
    eta:     f32    // Current eta (n1 / n2)
}

static AOV_PATH_NORMAL = 1;
static AOV_PATH_DIRECT = 2;
static AOV_PATH_NEE    = 3;

fn @wrap_ptraypayload(payload: PTRayPayload) -> RayPayload {
    let mut r : RayPayload;
    r.components(0) = payload.inv_pdf;
    r.components(1) = payload.contrib.r;
    r.components(2) = payload.contrib.g;
    r.components(3) = payload.contrib.b;
    r.components(4) = payload.depth as f32;
    r.components(5) = payload.eta;
    r
}

fn @unwrap_ptraypayload(payload: RayPayload) = PTRayPayload {
    inv_pdf = payload.components(0),
    contrib = make_color(payload.components(1), payload.components(2), payload.components(3), 1),
    depth   = payload.components(4) as i32,
    eta     = payload.components(5),
};

fn @init_pt_raypayload() = wrap_ptraypayload(PTRayPayload {
    inv_pdf = 0,
    contrib = color_builtins::white,
    depth   = 1,
    eta     = 1
});

fn @make_path_renderer(max_path_len: i32, num_lights: i32, lights: LightTable, light_selector: LightSelector, aovs: AOVTable, clamp_value: f32) -> Technique {
    let offset : f32  = 0.001;

    let aov_normal = @aovs(AOV_PATH_NORMAL);
    let aov_di     = @aovs(AOV_PATH_DIRECT);
    let aov_nee    = @aovs(AOV_PATH_NEE);

    let handle_color = if clamp_value > 0 {
        @|c: Color| color_saturate(c, clamp_value)
    } else {
        @|c: Color| c
    };

    // Only partially evaluate if number of lights is reasonable
    fn @(num_lights < 10) get_light(id : i32) -> Light {
        @lights(id)
    }

    fn @on_shadow( ray: Ray
                 , _pixel: i32
                 , _hit: Hit
                 , rnd: &mut RndState
                 , payload: RayPayload
                 , surf: SurfaceElement
                 , mat: Material
                 ) -> ShadowRay {
        // No shadow rays for specular materials
        if mat.bsdf.is_specular || num_lights == 0 {
            return(ShadowRay::None)
        }
        
        let pt = unwrap_ptraypayload(payload);
        if pt.depth + 1 > max_path_len {
            return(ShadowRay::None)
        }

        let (light_id, light_select_pdf) = light_selector.sample(rnd);

        let light         = get_light(light_id); 
        let sample_direct = light.sample_direct;
        let light_sample  = @sample_direct(rnd, surf);

        let pdf_l_s = light_sample.pdf.as_solid(light_sample.cos, light_sample.dist * light_sample.dist) * light_select_pdf; // Pdf to sample the light based on NEE
        if pdf_l_s <= flt_eps {
            return(ShadowRay::None)
        }

        let in_dir  = light_sample.dir; 
        let out_dir = vec3_neg(ray.dir);
        let dot     = vec3_dot(in_dir, surf.local.col(2));

        if dot > flt_eps && light_sample.cos > flt_eps {
            let mis = if light.delta { 
                1:f32
            } else {
                let pdf_e_s = mat.bsdf.pdf(in_dir, out_dir); // Pdf to sample the light based on bsdf
                1 / (1 + pdf_e_s / pdf_l_s)
            };

            // The intensity is already divided by the pdf, adapt to the (possible) change of domain
            let factor = light_sample.pdf.value / pdf_l_s;

            let contrib = handle_color(color_mulf(
                color_mul(light_sample.intensity, color_mul(pt.contrib, mat.bsdf.eval(in_dir, out_dir))), mis * factor));

            if light.infinite {
                return(make_simple_shadow_ray(
                    make_ray(surf.point, in_dir, offset, flt_max, ray_flag_shadow),
                    contrib
                ))
            } else {
                return(make_simple_shadow_ray(
                    make_ray(surf.point, vec3_sub(light_sample.pos, surf.point), offset, 1 - offset, ray_flag_shadow),
                    contrib
                ))
            }
        }
        ShadowRay::None
    }

    fn @on_hit( ray: Ray
              , pixel: i32
              , hit: Hit
              , payload: RayPayload
              , surf: SurfaceElement
              , mat: Material
              ) -> Option[Color] {
        let pt = unwrap_ptraypayload(payload);
        if pt.depth == 1 {
            aov_normal.splat(pixel, make_color(math_builtins::fabs(surf.local.col(2).x),
                                               math_builtins::fabs(surf.local.col(2).y),
                                               math_builtins::fabs(surf.local.col(2).z),
                                               1));
        }

        // Hits on a light source
        if mat.is_emissive && surf.is_entering {
            let dot = -vec3_dot(ray.dir, surf.local.col(2));
            if dot > flt_eps { // Only contribute proper aligned directions
                let emit    = mat.emission(ray);
                let pdf_s   = emit.pdf.as_solid(dot, hit.distance * hit.distance);
                let mis     = 1 / (1 + pt.inv_pdf * light_selector.pdf(emit.light_id) * pdf_s);
                let contrib = handle_color(color_mulf(color_mul(pt.contrib, emit.intensity), mis));
                
                aov_di.splat(pixel, contrib);

                return(make_option(contrib))
            }
        }
        Option[Color]::None
    }

    fn @on_miss( ray: Ray
               , pixel: i32
               , payload: RayPayload) -> Option[Color] {
        let mut inflights = 0;
        let mut color     = color_builtins::black;

        for light_id in safe_unroll(0, num_lights) {
            let light = @lights(light_id);
            // Do not include delta lights or finite lights
            if light.infinite && !light.delta {
                let pt = unwrap_ptraypayload(payload);

                inflights += 1;

                let emit  = light.emission(ray, make_invalid_surface_element());
                let pdf   = light.pdf_direct(ray, make_invalid_surface_element());
                let pdf_s = pdf.as_solid(1, 1/* We assume infinite lights are always given in solid angle measure */);
                let mis   = 1 / (1 + pt.inv_pdf * light_selector.pdf(light.id) * pdf_s);
                color     = color_add(color, handle_color(color_mulf(color_mul(pt.contrib, emit), mis)));
            }
        }

        if inflights > 0 {
            aov_di.splat(pixel, color);
            make_option(color)
        } else {
            Option[Color]::None
        }
    }

    fn @on_bounce( ray: Ray
                 , _pixel: i32
                 , _hit: Hit
                 , rnd: &mut RndState
                 , payload: RayPayload
                 , surf: SurfaceElement
                 , mat: Material
                 ) -> Option[(Ray, RayPayload)] {
        let pt = unwrap_ptraypayload(payload);
        
        if pt.depth + 1 > max_path_len {
            return(Option[(Ray, RayPayload)]::None)
        }

        // Bounce
        let out_dir = vec3_neg(ray.dir);
        if let Option[BsdfSample]::Some(mat_sample) = mat.bsdf.sample(rnd, out_dir, false) {
            let contrib = color_mul(pt.contrib, mat_sample.color/* Pdf and cosine are already applied!*/);
            let rr_prob = russian_roulette_pbrt(color_mulf(contrib, pt.eta * pt.eta), 0.95);
            if randf(rnd) >= rr_prob {
                return(Option[(Ray, RayPayload)]::None)
            }

            let inv_pdf     = if mat.bsdf.is_specular { 0 } else { 1 / mat_sample.pdf };
            let new_contrib = color_mulf(contrib, 1 / rr_prob);

            make_option(
                make_ray(surf.point, mat_sample.in_dir, offset, flt_max, ray_flag_bounce),
                wrap_ptraypayload(PTRayPayload {
                    inv_pdf = inv_pdf,
                    contrib = new_contrib,
                    depth   = pt.depth + 1,
                    eta     = pt.eta * mat_sample.eta
                })
            )
        } else {
            Option[(Ray, RayPayload)]::None
        }
    }

    fn @on_shadow_miss( _ray: Ray
                      , pixel: i32
                      , _shader: MaterialShader
                      , color: Color) -> Option[Color] {
        aov_nee.splat(pixel, color);
        make_option(color)
    }

    Technique {
        on_hit         = on_hit,
        on_miss        = on_miss,
        on_shadow      = on_shadow,
        on_bounce      = on_bounce,
        on_shadow_hit  = TechniqueNoShadowHitFunction,
        on_shadow_miss = on_shadow_miss,
    }
}
