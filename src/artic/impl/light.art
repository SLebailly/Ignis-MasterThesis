// This samples the environment by naive sphere sampling
fn @make_mix_light(lightA : Light, lightB : Light, probability : f32) = Light {
    sample_direct = @ |rnd, from| {
        let A = lightA.sample_direct(rnd, from);
        let B = lightB.sample_direct(rnd, from);
        let u = randf(rnd);
        if u < probability {
            make_direct_sample(A.posdir, A.intensity,
                 A.pdf_area*(1-probability)+B.pdf_area*probability, A.pdf_dir*(1-probability)+B.pdf_dir*probability, A.cos)
        } else {
            make_direct_sample(B.posdir, B.intensity,
                 A.pdf_area*(1-probability)+B.pdf_area*probability, A.pdf_dir*(1-probability)+B.pdf_dir*probability, B.cos)
        }
    },
    sample_emission = @ |rnd| {
        if !lightA.delta && !lightB.delta {
            let A = lightA.sample_emission(rnd);
            let B = lightB.sample_emission(rnd);
            let u = randf(rnd);

            if u < probability {
                make_emission_sample(A.pos, A.dir, A.intensity,
                     A.pdf_area*(1-probability)+B.pdf_area*probability, A.pdf_dir*(1-probability)+B.pdf_dir*probability, A.cos)
            } else {
                make_emission_sample(B.pos, B.dir, B.intensity,
                     A.pdf_area*(1-probability)+B.pdf_area*probability, A.pdf_dir*(1-probability)+B.pdf_dir*probability, B.cos)
            }
        } else if lightA.delta && !lightB.delta {
            lightB.sample_emission(rnd)
        } else {
            lightA.sample_emission(rnd)
        }
    },
    emission = @ |dir, prim_coords| {
        if !lightA.delta && !lightB.delta {
            let A = lightA.emission(dir, prim_coords);
            let B = lightB.emission(dir, prim_coords); 
            make_emission_value(color_lerp(A.intensity,B.intensity, probability), A.pdf_area*(1-probability)+B.pdf_area*probability, A.pdf_dir*(1-probability)+B.pdf_dir*probability) 
        } else if lightA.delta && !lightB.delta {
            lightB.emission(dir, prim_coords)
        } else {
            lightA.emission(dir, prim_coords)
        }
    },
    delta = (lightA.delta && lightB.delta),
    infinite = (lightA.infinite || lightB.infinite)
};

//-------------------------------------------
fn @make_null_light() = Light {
    sample_direct = @ |_, _| undef[DirectLightSample](),
    sample_emission = @ |_| undef[EmissionSample](),
    emission = @ |_, _| make_emission_value(black, 1, 1),
    delta    = true,
    infinite = true
};

//-------------------------------------------
fn @make_point_light(pos: Vec3, color: Color) = Light {
    sample_direct = @ |_, _| {
        let intensity = color_mulf(color, 1 / (4 * flt_pi));
        make_direct_sample(pos, intensity, 1, uniform_sphere_pdf(), 1)
    },
    sample_emission = @ |rnd| {
        let u = randf(rnd);
        let v = randf(rnd);
        let sample = sample_uniform_sphere(u, v);
        let intensity = color_mulf(color, 1 / (4 * flt_pi));
        make_emission_sample(pos, sample.dir, intensity, 1, sample.pdf, 1)
    },
    emission = @ |_, _| make_emission_value(black, 1, 1),
    delta    = true,
    infinite = false
};

//-------------------------------------------
fn @make_directional_light(dir: Vec3, max_radius: f32, color: Color) = Light {
    sample_direct = @ |_, _| {
        make_direct_sample(dir, color, 1, 1, 1)
    },
    sample_emission = @ |_| {
        make_emission_sample(vec3_mulf(dir, max_radius), dir, color, 1, 1, 1)
    },
    emission = @ |_, _| make_emission_value(black, 1, 1),
    delta    = true,
    infinite = true
};

//-------------------------------------------
fn @make_sun_light(dir: Vec3, max_radius: f32, sun_radius: f32, color: Color) = Light {
    sample_direct = @ |rnd, _| {
        let u = randf(rnd);
        let v = randf(rnd);
        let sample = sample_uniform_disk(u, v, sun_radius);
        let ndir = vec3_normalize(mat3x3_mul(make_orthonormal_mat3x3(dir), sample.pos));
        make_direct_sample(ndir, color, 1.0, sample.pdf, 1.0)
    },
    sample_emission = @ |rnd| {
        let u = randf(rnd);
        let v = randf(rnd);
        let sample = sample_uniform_disk(u, v, sun_radius);
        let ndir = vec3_normalize(mat3x3_mul(make_orthonormal_mat3x3(dir), sample.pos));
        make_emission_sample(vec3_mulf(dir, max_radius), ndir, color, 1.0, sample.pdf, 1.0)
    },
    emission = @ |_, _| make_emission_value(black, 1, 1),
    delta    = true,
    infinite = true
};

//-------------------------------------------
// This samples the environment by naive sphere sampling
fn @make_environment_light(max_radius: f32, color: Color) = Light {
    sample_direct = @ |rnd, _| {
        let u = randf(rnd);
        let v = randf(rnd);
        let sample = sample_uniform_sphere(u, v);
        make_direct_sample(sample.dir, color, 1.0, sample.pdf, 1.0)
    },
    sample_emission = @ |rnd| {
        let u = randf(rnd);
        let v = randf(rnd);
        let sample = sample_uniform_sphere(u, v);
        make_emission_sample(vec3_mulf(sample.dir,max_radius), sample.dir, color, 1.0, sample.pdf, 1.0)
    },
    emission = @ |_, _| make_emission_value(color, 1.0, uniform_sphere_pdf()),
    delta    = false,
    infinite = true
};

//-------------------------------------------
// This samples the environment by a given two-dimensional cdf
fn @make_environment_light_textured(max_radius: f32, tex: Texture, env_cdf: cdf::CDF2D, theta_off: f32, phi_off: f32) -> Light {
    let map = @|dir : Vec3| -> Vec2 {
        let (theta, phi) = spherical_from_dir(dir);
        let v = (theta + theta_off) / flt_pi;
        let u = (phi + phi_off) / (2 * flt_pi);
        make_vec2(u,v)
    };
    let unmap = @|uv : Vec2| -> Vec3 {
        let border = make_repeat_border();
        let theta  = border.vert(uv.y - theta_off / flt_pi) * flt_pi;
        let phi    = border.horz(uv.x - phi_off / (2 * flt_pi)) * 2 * flt_pi;
        dir_from_spherical(theta, phi)
    };
    let eval = @|dir : Vec3| tex(map(dir));

    Light {
        sample_direct = @ |rnd, _| {
            let u = randf(rnd);
            let v = randf(rnd);
            let sample    = cdf::sample_cdf_2d(make_vec2(u, v), env_cdf);
            let sinTheta  = math_builtins::sin(sample.pos.y*flt_pi);
            let dir       = unmap(sample.pos);
            let intensity = eval(dir);
            make_direct_sample(dir, intensity, 1.0, sample.pdf / (sinTheta * flt_pi * flt_pi * 2), 1.0)
        },
        sample_emission = @ |rnd| {
            let u = randf(rnd);
            let v = randf(rnd);
            let sample    = cdf::sample_cdf_2d(make_vec2(u, v), env_cdf);
            let sinTheta  = math_builtins::sin(sample.pos.y*flt_pi);
            let dir       = unmap(sample.pos);
            let intensity = eval(dir);
            make_emission_sample(vec3_mulf(dir, max_radius), dir, intensity, 1.0, sample.pdf / (sinTheta * flt_pi * flt_pi * 2), 1.0)
        },
        emission = @ |dir, _| {
            let uv        = map(dir);
            let pdf       = cdf::pdf_cdf_2d(uv, env_cdf);
            let sinTheta  = math_builtins::sin(uv.y*flt_pi);
            make_emission_value(eval(dir), 1.0, pdf / (sinTheta * flt_pi * flt_pi * 2))
        },
        delta    = false,
        infinite = true
    }
}

//-------------------------------------------
fn @make_environment_light_function(max_radius: f32, func: fn(Vec3)->Color) = Light {
    sample_direct = @ |rnd, _| {
        let u = randf(rnd);
        let v = randf(rnd);
        let sample = sample_uniform_sphere(u, v);
        let intensity = func(sample.dir);
        make_direct_sample(sample.dir, intensity, 1.0, sample.pdf, 1.0)
    },
    sample_emission = @ |rnd| {
        let u = randf(rnd);
        let v = randf(rnd);
        let sample = sample_uniform_sphere(u, v);
        let intensity = func(sample.dir);
        make_emission_sample(vec3_mulf(sample.dir,max_radius), sample.dir, intensity, 1.0, sample.pdf, 1.0)
    },
    emission = @ |dir, _| {
        make_emission_value(func(dir), 1.0, uniform_sphere_pdf())
    },
    delta    = false,
    infinite = true
};

// Some texture node do not have an associated cdf, use naive sampling instead
fn @make_environment_light_textured_naive(max_radius: f32, tex: Texture, theta_off: f32, phi_off: f32) =
    make_environment_light_function(max_radius, @|dir|{
        let (theta, phi) = spherical_from_dir(dir);
        let u = (theta + theta_off) / flt_pi;
        let v = (phi + phi_off) / (2 * flt_pi);

        tex(make_vec2(v,u))
});

fn @make_cie_sky_light(max_radius: f32, zenith: Color, ground: Color, groundBrightness: f32, cloudy: bool) =
    make_environment_light_function(max_radius, @|dir|{
        let theta = -dir.y;
        let a     = math_builtins::pow[f32](theta + 1.01, 10);
        let b	  = 1 / a;
        let denom = 1 / (a + b);
        let c1    = if cloudy { (1 + 2.0 * theta) / 3.0} else { 1 : f32 };
        let c2    = if cloudy { 0.777777777 : f32} else { 1 : f32 };
        color_mulf(color_add(color_mulf(zenith, c1 * a), color_mulf(ground, groundBrightness * c2 * b)), denom)
});

//-------------------------------------------
fn @make_perez_light(max_radius: f32, sun_dir: Vec3, lum: Color, a: f32, b: f32, c: f32, d: f32, e: f32)  =
    make_environment_light_function(max_radius, @|dir|{
        let theta = -dir.y;                     // Cosine between zenith and direction
        let sun_c = vec3_dot(dir, sun_dir);     // Cosine between sun and direction
        let sun_a = math_builtins::acos(sun_c); // Angle between sun and direction

        let A      = if math_builtins::fabs(theta) <= flt_eps { 1 } else { 1 + a * math_builtins::exp(b / theta) };
        let B      = 1 + c * math_builtins::exp(d * sun_a) + e * sun_c * sun_c;
        let factor = A * B;
        color_mulf(lum, factor)
});

//-------------------------------------------
fn @make_area_light(area: AreaEmitter, color_f: fn (Vec2) -> Color) = Light {
    sample_direct = @ |rnd, from_surf| {
        let (pos, n, coords, area_pdf) = area.sample(make_vec2(randf(rnd), randf(rnd)));
        let dir = vec3_sub(from_surf.point, pos);
        let cos = vec3_dot(dir, n) / vec3_len(dir);
        make_direct_sample(pos, color_f(coords), area_pdf, cosine_hemisphere_pdf(cos), cos)
    },
    sample_emission = @ |rnd| {
        let (pos, n, coords, area_pdf) = area.sample(make_vec2(randf(rnd), randf(rnd)));
        let sample = sample_cosine_hemisphere(randf(rnd), randf(rnd));
        make_emission_sample(pos, mat3x3_mul(make_orthonormal_mat3x3(n), sample.dir), color_f(coords), area_pdf, sample.pdf, sample.dir.z)
    },
    emission = @ |dir, surf| make_emission_value(color_f(surf.tex_coords), area.pdf(surf.prim_coords), cosine_hemisphere_pdf(vec3_dot(area.normal(surf.prim_coords), dir))),
    delta    = false,
    infinite = false
};

fn @make_shape_area_emitter(entity: Entity, shape: Shape) -> AreaEmitter {
    let mesh = shape.mesh;
    AreaEmitter {
        sample = @ |uv| {
            let ux  = uv.x * (mesh.num_tris as f32);
            let f   = ux as i32;
            let uv2 = make_vec2(ux - (f as f32), uv.y);
            let (i0, i1, i2) = mesh.triangles(f);
            let v0  = mesh.vertices(i0);
            let v1  = mesh.vertices(i1);
            let v2  = mesh.vertices(i2);
            let t0  = mesh.tex_coords(i0);
            let t1  = mesh.tex_coords(i1);
            let t2  = mesh.tex_coords(i2);
            let n   = mesh.face_normals(f);
            let (u, v) = sample_triangle(uv2.x, uv2.y);
            let inv_area = mesh.face_inv_area(f);
            (mat3x4_transform_point(entity.global_mat, vec3_lerp2(v0, v1, v2, u, v)),
             vec3_normalize(mat3x3_mul(entity.normal_mat, n)),
             vec2_lerp2(t0, t1, t2, u, v),
             inv_area)
        },
        normal = @ |uv| {
            let f = (uv.x * (mesh.num_tris as f32)) as i32;
            vec3_normalize(mat3x3_mul(entity.normal_mat, mesh.face_normals(f)))
        },
        pdf = @ |uv| {
            let f = (uv.x * (mesh.num_tris as f32)) as i32;
            mesh.face_inv_area(f)
        }
    }
}