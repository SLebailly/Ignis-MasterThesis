fn @shading_normal_adjoint(in_dir: Vec3, out_dir: Vec3, ns: Vec3, ng: Vec3) -> f32 {
    let ons = positive_cos(out_dir, ns);
    let ins = positive_cos(in_dir, ns);
    let ong = positive_cos(out_dir, ng);
    let ing = positive_cos(in_dir, ng);
    if ins <= flt_eps || ong <= flt_eps { 0 } else { (ons/ins) * (ing/ong) }
}

fn @transform_surf_bsdf(old_surf: SurfaceElement, new_surf: SurfaceElement, bsdf_factory: fn (SurfaceElement) -> Bsdf) -> Bsdf {
    let bsdf = @bsdf_factory(new_surf);

    Bsdf {
        eval   = bsdf.eval,
        pdf    = bsdf.pdf,
        sample = @ |rnd, out_dir, adjoint| {
            if let Option[BsdfSample]::Some(s) = bsdf.sample(rnd, out_dir, adjoint) {
                if adjoint {
                    let fac = shading_normal_adjoint(s.in_dir, out_dir, new_surf.local.col(2), old_surf.local.col(2));
                    make_bsdf_sample(s.in_dir, s.pdf, color_mulf(s.color, fac), s.eta)
                } else {
                    make_option(s)
                }
            } else {
                reject_bsdf_sample()
            }
        },
        is_specular = bsdf.is_specular
    }
}

//--------------------------------------
// Special bsdf replacing the normal orientation
fn @make_normal_set(surf: SurfaceElement, bsdf_factory: fn (SurfaceElement) -> Bsdf, normal: Vec3) -> Bsdf {
    let new_surf = SurfaceElement{
        is_entering = surf.is_entering,
        point       = surf.point,
        face_normal = surf.face_normal,
        inv_area    = surf.inv_area,
        prim_coords = surf.prim_coords,
        tex_coords  = surf.tex_coords,
        local       = make_orthonormal_mat3x3(normal)
    };
    transform_surf_bsdf(surf, new_surf, bsdf_factory)
}

//--------------------------------------
// Special bsdf replacing the normal and tangent orientation
fn @make_normal_tangent_set(surf: SurfaceElement, bsdf_factory: fn (SurfaceElement) -> Bsdf, normal: Vec3, tangent: Vec3) -> Bsdf {
    let new_surf = SurfaceElement{
        is_entering = surf.is_entering,
        point       = surf.point,
        face_normal = surf.face_normal,
        inv_area    = surf.inv_area,
        prim_coords = surf.prim_coords,
        tex_coords  = surf.tex_coords,
        local       = make_mat3x3(tangent, vec3_cross(tangent, normal), normal)
    };
    transform_surf_bsdf(surf, new_surf, bsdf_factory)
}

//--------------------------------------
// Normal Mapping
// Normal given in [0, 1] RGB fashion
fn @make_normalmap(surf: SurfaceElement, bsdf_factory: fn (SurfaceElement) -> Bsdf, normal: Color, strength: f32) -> Bsdf {
    let oN = vec3_normalize(mat3x3_left_mul(surf.local, vec3_normalize(make_vec3(2 * normal.r - 1, 2 * normal.g - 1, 2 * normal.b - 1))));
    let N  = if strength != 1 { vec3_normalize(vec3_add(surf.local.col(2), vec3_mulf(vec3_sub(oN, surf.local.col(2)), strength))) } else { oN };
    make_normal_set(surf, bsdf_factory, N)
}

//--------------------------------------
// Bump Mapping
fn @make_bumpmap(surf: SurfaceElement, bsdf_factory: fn (SurfaceElement) -> Bsdf, dx: f32, dy: f32, strength: f32) -> Bsdf {
    let N = vec3_normalize(vec3_sub(surf.local.col(2), vec3_mulf(vec3_add(vec3_mulf(surf.local.col(0), dx), vec3_mulf(surf.local.col(1), dy)), strength)));
    make_normal_set(surf, bsdf_factory, N)
}

