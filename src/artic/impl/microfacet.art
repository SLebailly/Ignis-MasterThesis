struct MicrofacetSample {
    normal: Vec3,          
    pdf   : f32
}

struct MicrofacetModel {
    D       : fn (Vec3) -> f32,
    G1      : fn (Vec3, Vec3) -> f32,
    sample  : fn (&mut RndState) -> MicrofacetSample,
    alpha_u : f32,
    alpha_v : f32,
    is_delta: bool
}

fn @g_1_walter(local: Mat3x3, w: Vec3, _m: Vec3, alpha_u: f32, alpha_v: f32) -> f32 {
    let cosZ = vec3_dot(local.col(2), w);
    if  math_builtins::fabs(cosZ) <= flt_eps { return(0) };

    let cosX = vec3_dot(local.col(0), w);
    let cosY = vec3_dot(local.col(1), w);

    let kx = alpha_u * cosX;
    let ky = alpha_v * cosY;
    let k2 = (kx * kx + ky * ky) / (cosZ * cosZ);

    if k2 <= flt_eps { return(1) };

    let a  = 1 / math_builtins::sqrt(k2);
    let a2 = 1 / k2;

    if a >= 1.6 { 
        1.0
    } else { 
        (3.535 * a + 2.181 * a2) / (1.0 + 2.276 * a + 2.577 * a2) 
    }
}

fn @g_1_smith(local: Mat3x3, w: Vec3, _m: Vec3, alpha_u: f32, alpha_v: f32) -> f32 {
    let cosZ = vec3_dot(local.col(2), w);
    if math_builtins::fabs(cosZ) <= flt_eps { return(0) };

    let cosX = vec3_dot(local.col(0), w);
    let cosY = vec3_dot(local.col(1), w);

    let kx = alpha_u * cosX;
    let ky = alpha_v * cosY;
    let a2 = kx * kx + ky * ky;
    if a2 <= flt_eps {
        1
    } else {
        let k2 = a2 / (cosZ * cosZ);
        let denom = 1 + math_builtins::sqrt(1 + k2);
        2 / denom
    }
}

fn @ndf_beckmann(local: Mat3x3, m: Vec3, alpha_u: f32, alpha_v: f32) -> f32 {
    let cosZ = vec3_dot(local.col(2), m);
    let cosX = vec3_dot(local.col(0), m);
    let cosY = vec3_dot(local.col(1), m);

    let kx = cosX / alpha_u;
    let ky = cosY / alpha_v;
    let k2 = safe_div(kx * kx + ky * ky, cosZ * cosZ);

    safe_div(math_builtins::exp(-k2), flt_pi * alpha_u * alpha_v * cosZ * cosZ * cosZ * cosZ)
}

fn @ndf_ggx(local: Mat3x3, m: Vec3, alpha_u: f32, alpha_v: f32) -> f32 {
    let cosZ = vec3_dot(local.col(2), m); // cosTheta
    let cosX = vec3_dot(local.col(0), m); // cosPhi * sinTheta
    let cosY = vec3_dot(local.col(1), m); // sinPhi * sinTheta

    let kx = cosX / alpha_u;
    let ky = cosY / alpha_v;
    let k  = kx * kx + ky * ky + cosZ * cosZ;

    safe_div(1, flt_pi * alpha_u * alpha_v * k * k)
}

fn @make_delta_model(local: Mat3x3) = MicrofacetModel {
    D  = @|_| 0,
    G1 = @|_, _| 0,
    sample = @ |_| {
        MicrofacetSample { normal = local.col(2), pdf = 1}
    },
    alpha_u  = 0,
    alpha_v  = 0,
    is_delta = true
};

fn @make_aniso_beckmann_model(local: Mat3x3, alpha_u: f32, alpha_v: f32) = MicrofacetModel {
    D  = @|m| ndf_beckmann(local, m, alpha_u, alpha_v),
    G1 = @|w, m| g_1_walter(local, w, m, alpha_u, alpha_v),
    sample = @ |rnd| {
        let u0 = randf(rnd);
        let u1 = randf(rnd);
        
        let ar     = alpha_v / alpha_u;
        let phi    = math_builtins::atan(ar * math_builtins::tan(2 * flt_pi * u1));
        let cosPhi = math_builtins::cos(phi);
        let sinPhi = math_builtins::sqrt(1 - cosPhi*cosPhi);

        let kx = cosPhi / alpha_u;
        let ky = sinPhi / alpha_v;
        let k2 = 1 / (kx * kx + ky * ky);

        let cosTheta  = 1 / math_builtins::sqrt(1 - k2 * math_builtins::log(1.0 - u0));
        let cosTheta2 = cosTheta * cosTheta;
        let sinTheta  = math_builtins::sqrt(1 - cosTheta2);

        let pdf = (1 - u0) / (flt_pi * alpha_u * alpha_v * cosTheta2 * cosTheta);
        let lh  = make_vec3(sinTheta * cosPhi, sinTheta * sinPhi, cosTheta); // Microfacet normal in local shading space
        MicrofacetSample { normal = mat3x3_mul(local, lh), pdf = pdf}
    },
    alpha_u  = alpha_u,
    alpha_v  = alpha_v,
    is_delta = false
};
fn @make_iso_beckmann_model(local: Mat3x3, alpha: f32) = make_aniso_beckmann_model(local, alpha, alpha);

fn @make_aniso_ggx_model(local: Mat3x3, alpha_u: f32, alpha_v: f32) = MicrofacetModel {
    D  = @|m| ndf_ggx(local, m, alpha_u, alpha_v),
    G1 = @|w, m| g_1_smith(local, w, m, alpha_u, alpha_v),
    sample = @ |rnd| {
        let u0 = randf(rnd);
        let u1 = randf(rnd);
        
        let ar     = alpha_v / alpha_u;
        let phi    = if ?alpha_u && ?alpha_v && alpha_u == alpha_v { 2 * flt_pi * u1 } else { math_builtins::atan(ar * math_builtins::tan(2 * flt_pi * u1)) };
        let cosPhi = math_builtins::cos(phi);
        let sinPhi = math_builtins::sqrt(1 - cosPhi*cosPhi);

        let kx = cosPhi / alpha_u;
        let ky = sinPhi / alpha_v;
        let d2 = kx * kx + ky * ky;
        let a2 = safe_div(1, d2);
        let t2 = a2 * u0 / (1 - u0);

        let cosTheta  = 1 / math_builtins::sqrt(1 + t2);
        let cosTheta2 = cosTheta * cosTheta;
        let sinTheta  = math_builtins::sqrt(1 - cosTheta2);

        let k2  = d2 * (sinTheta * sinTheta) / cosTheta2;
        let pdf = safe_div(1, flt_pi * alpha_u * alpha_v * cosTheta2 * cosTheta * (1 + k2) * (1 + k2));
        let lh  = make_vec3(sinTheta * cosPhi, sinTheta * sinPhi, cosTheta);
        MicrofacetSample { normal = mat3x3_mul(local, lh), pdf = pdf}
    },
    alpha_u  = alpha_u,
    alpha_v  = alpha_v,
    is_delta = false
};
fn @make_iso_ggx_model(local: Mat3x3, alpha: f32) = make_aniso_ggx_model(local, alpha, alpha);
///////////////////////////////////

struct MicrofacetDistribution {
    D       : fn (Vec3) -> f32,             // Normal
    G       : fn (Vec3, Vec3, Vec3) -> f32, // Wi, Wo, Normal
    norm    : fn (Vec3, Vec3, Vec3) -> f32, // Wi, Wo, Normal
    pdf     : fn (Vec3, Vec3, Vec3) -> f32, // Wi, Wo, Normal
    sample  : fn (&mut RndState, Vec3) -> MicrofacetSample, // RND, Wo
    alpha_u : f32,
    alpha_v : f32,
    is_delta: bool
}

fn @compute_microfacet_norm(wi: Vec3, wo: Vec3, m: Vec3, local: Mat3x3) = safe_div(vec3_dot(wi, m), vec3_dot(local.col(2), wo));

fn @make_microfacet_distribution(model: MicrofacetModel, local: Mat3x3) = MicrofacetDistribution {
    D        = model.D,
    G        = @ |wi, wo, m| model.G1(wi, m) * model.G1(wo, m),
    norm     = @ |wi, wo, m| compute_microfacet_norm(wi, wo, m, local),
    pdf      = @ |_, _, m| model.D(m) * absolute_cos(local.col(2), m),
    sample   = @ |rnd, _| model.sample(rnd),
    alpha_u  = model.alpha_u,
    alpha_v  = model.alpha_v,
    is_delta = model.is_delta
};

fn @check_if_delta_distribution(alpha_u: f32, alpha_v: f32) = alpha_u <= 1e-4 || alpha_v <= 1e-4;

fn @make_delta_distribution(local: Mat3x3) = make_microfacet_distribution(make_delta_model(local), local);

fn @make_beckmann_distribution(local: Mat3x3, alpha_u: f32, alpha_v: f32) -> MicrofacetDistribution {
    if check_if_delta_distribution(alpha_u, alpha_v) {
        make_delta_distribution(local) 
    } else {
        make_microfacet_distribution(make_aniso_beckmann_model(local, alpha_u, alpha_v), local)
    }
}

fn @make_ggx_distribution(local: Mat3x3, alpha_u: f32, alpha_v: f32) -> MicrofacetDistribution {
    if check_if_delta_distribution(alpha_u, alpha_v) {
        make_delta_distribution(local)
    } else { 
        make_microfacet_distribution(make_aniso_ggx_model(local, alpha_u, alpha_v), local)
    }
}

// Based on:
// Journal of Computer Graphics Techniques Vol. 7, No. 4, 2018 http://jcgt.org.
// Sampling the GGX Distribution of Visible Normals. Eric Heitz
fn @sample_vndf_ggx_11(rnd: &mut RndState, cos_theta: f32) -> Vec2 {
    let u0 = randf(rnd);
    let u1 = randf(rnd);

	// Sample concentric uniform disk
    let pc = vec3_to_2(sample_uniform_disk(u0, u1, 1).pos);

    // Rescale
	let s = 0.5 * (1 + cos_theta);
	let y = (1 - s) * safe_sqrt(1 - pc.x * pc.x) + s * pc.y;

	// Project
    let z = safe_sqrt(1 - y*y - pc.x*pc.x);
    let sin_theta = safe_sqrt(1 - cos_theta * cos_theta);
    let norm = safe_div(1, sin_theta * y + cos_theta * z);
	
    vec2_mulf(make_vec2(cos_theta * y - sin_theta * z, pc.x), norm)
}

fn @sample_vndf_ggx(rnd: &mut RndState, local: Mat3x3, vN: Vec3, alpha_u: f32, alpha_v: f32) -> Vec3 {
    // Transform to local
    let vL = shading::to_local(local, vN);

    // Stretch
    let sL = vec3_normalize(make_vec3(alpha_u * vL.x, alpha_v * vL.y, vL.z));
    
    // Compute in slope space
    let (sin_phi, cos_phi) = shading::sin_cos_phi(sL);
    let slope = sample_vndf_ggx_11(rnd, shading::abs_cos_theta(sL));

    // Unstretch
    let slope2 = make_vec2(
        (cos_phi * slope.x - sin_phi * slope.y) * alpha_u,
        (sin_phi * slope.x + cos_phi * slope.y) * alpha_v
    );
    
	// Compute normal
	let Nh = vec3_normalize(make_vec3(-slope2.x, -slope2.y, 1));

    // Transform back to global
	shading::to_world(local, Nh)
}

fn @pdf_vndf_ggx(local: Mat3x3, w: Vec3, h: Vec3, alpha_u: f32, alpha_v: f32) -> f32 {
    let cosZ = absolute_cos(local.col(2), w);
    safe_div(g_1_smith(local, w, h, alpha_u, alpha_v) * absolute_cos(w, h) * ndf_ggx(local, h, alpha_u, alpha_v), cosZ)
}

fn @make_vndf_ggx_distribution(local: Mat3x3, alpha_u: f32, alpha_v: f32) -> MicrofacetDistribution {
    if check_if_delta_distribution(alpha_u, alpha_v) {
        make_delta_distribution(local)
    } else {
        let model = make_aniso_ggx_model(local, alpha_u, alpha_v);
        MicrofacetDistribution {
            D      = model.D,
            G      = @ |wi, wo, m| model.G1(wi, m) * model.G1(wo, m),
            norm   = @ |wi, wo, m| compute_microfacet_norm(wi, wo, m, local),
            pdf    = @ | _, wo, m| pdf_vndf_ggx(local, wo, m, alpha_u, alpha_v),
            sample = @ |rnd, wo|   {
                let m   = sample_vndf_ggx(rnd, local, wo, alpha_u, alpha_v);
                let pdf = pdf_vndf_ggx(local, wo, m, alpha_u, alpha_v);
                MicrofacetSample { normal = m, pdf = pdf }
            },
            alpha_u  = alpha_u,
            alpha_v  = alpha_v,
            is_delta = false
        }
    }
}