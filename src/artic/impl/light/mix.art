fn @make_mix_light(id: i32, lightA : Light, lightB : Light, probability : f32) = Light {
    id = id,
    sample_direct = @ |rnd, from| {
        let A = lightA.sample_direct(rnd, from);
        let B = lightB.sample_direct(rnd, from);
        let u = randf(rnd);
        if u < probability {
            make_direct_sample(A.posdir, A.intensity,
                lerp(A.pdf, B.pdf, probability), A.cos)
        } else {
            make_direct_sample(B.posdir, B.intensity,
                lerp(A.pdf, B.pdf, probability), B.cos)
        }
    },
    sample_emission = @ |rnd| {
        if !lightA.delta && !lightB.delta {
            let A = lightA.sample_emission(rnd);
            let B = lightB.sample_emission(rnd);
            let u = randf(rnd);

            if u < probability {
                make_emission_sample(A.pos, A.dir, A.intensity,
                    lerp(A.pdf_area, B.pdf_area, probability), lerp(A.pdf_dir, B.pdf_dir, probability), A.cos)
            } else {
                make_emission_sample(B.pos, B.dir, B.intensity,
                    lerp(A.pdf_area, B.pdf_area, probability), lerp(A.pdf_dir, B.pdf_dir, probability), B.cos)
            }
        } else if lightA.delta && !lightB.delta {
            lightB.sample_emission(rnd)
        } else {
            lightA.sample_emission(rnd)
        }
    },
    emission = @ |dir, prim_coords| {
        if !lightA.delta && !lightB.delta {
            let A = lightA.emission(dir, prim_coords);
            let B = lightB.emission(dir, prim_coords); 
            color_lerp(A, B, probability)
        } else if lightA.delta && !lightB.delta {
            lightB.emission(dir, prim_coords)
        } else {
            lightA.emission(dir, prim_coords)
        }
    },
    pdf_direct = @ |dir, prim_coords| {
        if !lightA.delta && !lightB.delta {
            let A = lightA.pdf_direct(dir, prim_coords);
            let B = lightB.pdf_direct(dir, prim_coords); 
            lerp(A, B, probability)
        } else if lightA.delta && !lightB.delta {
            lightB.pdf_direct(dir, prim_coords)
        } else {
            lightA.pdf_direct(dir, prim_coords)
        }
    },
    pdf_emission = @ |dir, prim_coords| {
        if !lightA.delta && !lightB.delta {
            let A = lightA.pdf_emission(dir, prim_coords);
            let B = lightB.pdf_emission(dir, prim_coords); 
            make_pdf_value(lerp(A.pdf_area, B.pdf_area, probability), lerp(A.pdf_dir, B.pdf_dir, probability)) 
        } else if lightA.delta && !lightB.delta {
            lightB.pdf_emission(dir, prim_coords)
        } else {
            lightA.pdf_emission(dir, prim_coords)
        }
    },
    delta    = (lightA.delta && lightB.delta),
    infinite = (lightA.infinite || lightB.infinite)
};
